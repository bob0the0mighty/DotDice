<roll>              ::= <basic_roll> | <grouped_roll>

<basic_roll>        ::= <number_of_dice> "d" <die_type_section> <modifiers>

<grouped_roll>      ::= "{" <roll_group> ("," <roll_group>)* "}" <modifiers>

<roll_group>        ::= <basic_roll> | <constant>

<constant>          ::= <integer>

<number_of_dice>    ::= <integer>
                      | ""  // Empty string represents 1 die

<die_type_section>  ::= <integer>
                      | "%"
                      | "F"

<modifiers>         ::= (<modifier>)* // Zero or more modifiers

<modifier>          ::= <keep_modifier>
                      | <drop_modifier>
                      | <reroll_modifier>
                      | <explode_modifier>
                      | <compounding_modifier>
                      | <success_modifier>
                      | <failure_modifier>
                      | <sort_modifier>

<keep_modifier>     ::= "kl" <keep_drop_value> //keep keep_drop_value lowest results
                      | "kh" <keep_drop_value> //keep keep_drop_value highest results

<drop_modifier>     ::= "dl" <keep_drop_value> //drop keep_drop_value lowest results
                      | "dh" <keep_drop_value> //drop keep_drop_value highest results

<keep_drop_value>   ::= <integer>
                      | "" //Empty string indicates default behavior

<reroll_modifier>   ::= "r" <comparison_point> // continuesly re-roll any result that meets comparison_point
                      | "ro" <comparison_point> // re-roll once any result that meets comparison_point

<explode_modifier>  ::= "!" <comparison_point> // add an additional dice role for every comparison_point result

<compounding_modifier> ::= "!!" <comparison_point> //if comparison_point is met, then roll another dice and add it to the total. This new roll can compound also.

<success_modifier>  ::= <comparison_point>

<failure_modifier>  ::= "f" <comparison_point> 

<sort_modifier>     ::= "sa"
                      | "sd"

<comparison_point>  ::= <comparison_operator> <integer>
                      | <integer> // Default is "="

<comparison_operator> ::= "=" | ">" | "<"

<integer>           ::= ["-"] <digit>+

<digit>             ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"